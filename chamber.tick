dbrp "chambers"."autogen"

var interval = 10m
var oneday = 18h
var message_dead = '[OFF] {{ .ID }}'
var message_alive = '[OK] {{ .ID }}'
var message_difference = '[CRIT] {{ .ID }} {{ index .Fields "temp_diff" | printf "%.2f" }}'
var message_difference_ok = '[OK] {{ .ID }}'
var message_spread = '[CRIT] {{ .ID }} {{ index .Fields "temp_recorded_spread" | printf "%.2f" }}'
var message_spread_ok = '[OK] {{ .ID }}'

var data = stream
  |from()
    .retentionPolicy('autogen')
    .database('chambers')
    .measurement('conviron')
    .groupBy('host')

var temp_diff_alert = data
  |window()
    .period(30m)
  |eval(lambda: min(abs("temp_target" - "temp_recorded"))) // get abs temperature difference
    .as('temp_diff')
  |alert()
    .crit(lambda: "temp_diff" > 2.0) // abs temperature difference shouldnt be over 2
    .id('{{ index .Tags "host" }} chamber temperature diff')
    .message('{{ if eq .Level "OK" }}' + message_difference_ok + '{{ else }}' + message_difference + '{{ end }}.')
    .exec('python3', '/home/gareth/tickscripts/gitalert.py')
    .slack()
    .stateChangesOnly()

var temp_spread_alert = data
  |window()
    .align()
    .period(oneday)
    .every(interval)
    .fillPeriod()
  |spread('temp_recorded')
    .as('temp_recorded_spread')
  |alert()
    .crit(lambda: "temp_recorded_spread" < 3.0)
    .id('{{ index .Tags "host" }} chamber temperature spread')
    .message('{{ if eq .Level "OK" }}' + message_spread_ok + '{{ else }}' + message_spread + '{{ end }}.')
    .exec('python3', '/home/gareth/tickscripts/gitalert.py')
    .slack()
    .stateChangesOnly()

var par_spread_alert = data
  |window()
    .align()
    .period(oneday)
    .every(interval)
    .fillPeriod()
  |spread('par')
    .as('par_spread')
  |alert()
    .crit(lambda: "par_spread" < 5.0)
    .id('{{ index .Tags "host" }} par spread')
    .message('{{ if eq .Level "OK" }}' + message_spread_ok + '{{ else }}' + message_spread + '{{ end }}.')
    .slack()
    .exec('python3', '/home/gareth/tickscripts/gitalert.py')
    .stateChangesOnly()

var dman = data
  |deadman(0.0, 30m)
    .id('{{ index .Tags "host" }} chamber deadman')
    .message('{{ if eq .Level "OK" }}' + message_alive + '{{ else }}' + message_dead + '{{ end }}.')
    .slack()
    .exec('python3', '/home/gareth/tickscripts/gitalert.py')
    .stateChangesOnly()

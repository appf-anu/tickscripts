dbrp "chambers"."autogen"

var message_critok = '{{ if eq .Level "OK" }}[OK] {{ .ID }}{{ else }}[CRIT] {{ .ID }}{{ end }}'

var deets = '
 HiPressure: {{index .Fields "HiPressure.min" }}psi
 LoPressure: {{index .Fields "LoPressure.max" }}psi
 Difference: {{index .Fields "PressureDiff"  | printf "%.1f"}}psi'

var data = stream
  |from()
    .retentionPolicy('autogen')
    .database('chambers')
    .measurement('conviron2')
  |window()
    .period(10m)
    .every(5m)
    .fillPeriod()
    .align()

var LoPressureMax = data
  |max('LoPressure')

var newData = data
  |min('HiPressure')
  |join(LoPressureMax)
    .as('HiPressure', 'LoPressure')
  |eval(lambda: float("HiPressure.min") - float("LoPressure.max"))
    .as('PressureDiff')
    .keep('HiPressure.min', 'LoPressure.max', 'PressureDiff')
  |alert()
    .crit(lambda: "PressureDiff" < 0.0) // abs pressure difference shouldnt be under 0
    .id('Facility Pressure Difference')
    .message(message_critok)
    .details(deets)
    .exec('python3', '/etc/kapacitor/python/gitalert.py')
    .stateChangesOnly()